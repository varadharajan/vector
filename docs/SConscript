Import('env')

build_appendix = Builder(action='docs/build-appendix.sh $SOURCES > $TARGET')
pandoc = Builder(action='pandoc $PDFLAGS $SOURCES -o $TARGET')

env.Append(BUILDERS = {'Pandoc': pandoc, 'Appendix': build_appendix})
env.Append(PDFLAGS = '-V "geometry=margin=1in"')

compiler_source_files = ['scanner.mll', 'parser.mly', 'ast.ml', 'symgen.ml',
                         'environment.ml', 'detect.ml', 'generator.ml']
rtlib_source_files = ['libvector.hpp', 'vector_array.hpp',
                      'vector_utils.hpp', 'vector_iter.hpp']

source_file_paths = ['../compiler/' + sf for sf in compiler_source_files] + [
                     '../rtlib/' + sf for sf in rtlib_source_files]

env.Appendix('appendix-a.md', source_file_paths)
env.Pandoc('final-report.pdf',
        ['introduction.md', 'language-tutorial.md', 'language-ref.md',
         'project-plan.md', 'architectural-design.md', 'test-plan.md',
         'lessons-learned.md', 'appendix-a.md'])
